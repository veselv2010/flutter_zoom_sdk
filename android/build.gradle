group 'com.evilratt.flutter_zoom_sdk'
version '1.0'

buildscript {
    ext.zoomSdkVersion = '6.0.12.22275'

    repositories {
        google()
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        consumerProguardFiles 'proguard-rules.pro'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
        compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dependencies {
        implementation "androidx.legacy:legacy-support-v4:1.0.0"
        implementation "androidx.security:security-crypto:1.1.0-alpha05"
        implementation "com.google.crypto.tink:tink-android:1.7.0"
        implementation "com.google.android.exoplayer:exoplayer-core:2.17.1"
        implementation "com.google.android.exoplayer:exoplayer-ui:2.17.1"
        implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

        implementation "androidx.appcompat:appcompat:1.6.1"
        implementation "androidx.constraintlayout:constraintlayout:2.1.0"
        implementation "com.google.android.material:material:1.9.0"
        implementation "com.google.android.flexbox:flexbox:3.0.0"
        implementation "androidx.multidex:multidex:2.0.1"
        implementation "com.google.code.gson:gson:2.9.1"
        implementation "com.github.bumptech.glide:annotations:4.11.0"
        implementation "com.github.bumptech.glide:glide:4.11.0"
        annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
        implementation "org.greenrobot:eventbus:3.1.1"

        implementation "androidx.recyclerview:recyclerview:1.2.1"
        implementation "com.airbnb.android:lottie:4.0.0"

        implementation "androidx.window:window:1.1.0"

        implementation "androidx.window:window-java:1.1.0"

        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.20"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.20"

        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
        implementation "androidx.core:core-ktx:1.8.0"
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"

        implementation "androidx.fragment:fragment-ktx:1.4.1"
        implementation "com.davemorrissey.labs:subsampling-scale-image-view:3.10.0"
        implementation "androidx.core:core-splashscreen:1.0.1"
        implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
        implementation "androidx.webkit:webkit:1.6.0"

        implementation "io.reactivex.rxjava3:rxandroid:3.0.2"
        implementation "androidx.compose.material3:material3:1.1.1"
        implementation "androidx.compose.ui:ui-tooling-preview:1.4.1"
        implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
        implementation "androidx.activity:activity-compose:1.6.1"
        implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"
        implementation "androidx.navigation:navigation-compose:2.6.0"
        implementation "io.coil-kt:coil-compose:2.3.0"

        implementation "us.zoom.meetingsdk:mobilertc:$zoomSdkVersion"
    }

    namespace 'com.evilratt.flutter_zoom_sdk'
}

publishing {
    publications {
        mobilertc(MavenPublication) {
            groupId = 'us.zoom.meetingsdk'
            artifactId = 'mobilertc'
            version = zoomSdkVersion
            artifact file("$projectDir/libs/mobilertc.aar")
        }
    }
    
    repositories {
        mavenLocal()
    }
}

tasks.register('downloadZoomSdk', Copy) {
    def logPrefix = 'flutter_zoom_sdk:downloadZoomSdk'
    def zipFileName = "zoom-sdk-android-${zoomSdkVersion}.zip"
    def url = "https://zoommeetingsdk.blob.core.windows.net/android/${zipFileName}"

    def libsDir = file('libs')
    def zipFile = file("${libsDir}/${zipFileName}")
    def markerFile = file("${libsDir}/.${zipFileName}.downloaded")

    println("${logPrefix}: url: ${url}")
    println("${logPrefix}: zip file path: ${zipFile}")

    into(libsDir) {
        include '*.aar'
        eachFile { file -> file.delete() }
    }

    if (!markerFile.exists()) {
        if (zipFile.exists()) {
            zipFile.delete()
            println("${logPrefix}: the file ${zipFileName} was removed")
        }

        if (!zipFile.exists()) {
            libsDir.mkdirs()

            println("${logPrefix}: starting download of ${zipFileName}...")

            zipFile.withOutputStream { outputStream ->
                new URL(url).withInputStream { inputStream ->
                    outputStream << inputStream
                }
            }
        }

        markerFile.createNewFile()
        println("${logPrefix}: download of ${zipFileName} completed")
    } else {
        println("${logPrefix}: marker file already exist: ${markerFile}")
    }

    // Extract the ZIP file
    from(zipTree(zipFile)) {
        include '**/*.aar'
        eachFile { file -> file.path = file.name }
    }

    into libsDir
    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}


publishMobilertcPublicationToMavenLocalRepository.dependsOn(downloadZoomSdk)
preBuild.dependsOn(publish)